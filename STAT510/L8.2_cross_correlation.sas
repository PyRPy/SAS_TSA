data soi;
   infile 'C:\Users\OneDrive\Documents\STAT_PSU\STAT510\SAS501\Data\soi.dat' dlmstr=' ';
   input soi @@;
   t=_N_; *because we do not have dates in our data;
run;
data rec;
   infile 'C:\Users\OneDrive\Documents\STAT_PSU\STAT510\SAS501\Data\recruit.dat' dlmstr=' ';
   input rec @@;
   t=_N_; *because we do not have dates in our data;
run;
data soif;
  merge soi rec;
  by t;
run;
proc timeseries data=soif crossplots=ccf outcrosscorr=soifccf;
  crosscorr ccf;
  crossvar soi rec;
  *SAS calculates 2 sample CCF plots. Within the soi folder, there is a rec subfolder with a ccf plot of the set of sample
  correlations between soi_t and rec_{t+h} as we see in R.  Within the rec folder, there is a soi subfolder with a ccf 
  plot of the set of sample correlations between rec_t and soi_{t+h}. The “x leading y” side of the plot is for h 
  positive as you see in Minitab.;
run;
data soif;
  set soif;
  lag1soi=lag(soi);
  lag2soi=lag2(soi);
  lag3soi=lag3(soi);
  lag4soi=lag4(soi);
  lag5soi=lag5(soi);
  lag6soi=lag6(soi);
  lag7soi=lag7(soi);
  lag8soi=lag8(soi);
  lag9soi=lag9(soi);
  lag10soi=lag10(soi);
  lag1rec=lag(rec);
  lag2rec=lag2(rec);
run;
proc sgscatter data=soif;
  title "Scatterplots";
  plot (rec)*(soi lag1soi lag2soi lag3soi lag4soi lag5soi lag6soi lag7soi lag8soi lag9soi lag10soi);
run;
proc reg data=soif;
  model rec = lag1rec lag2rec lag5soi lag6soi / r;
  output out=soifres r=residuals;
run; quit;
title "ACF/PACF of Residuals";
proc timeseries data=soifres plots=(acf pacf);
  var residuals;
run;
