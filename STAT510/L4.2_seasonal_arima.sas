libname sas510 'X:\My Documents\sas\510'; *if you choose to permanently store any data or output from below;
data flow;
   infile 'C:\Users\Documents\My SAS Files\TS_SAS\Data\coloradoflow.dat' dlmstr=' ';
   input flow @@;
   t=_N_; *because we do not have dates in our data;
run;
proc print;
run;
title "Plot of Colorado Flow";
proc timeseries data=flow seasonality=12 plots=(series);
  var flow;
run;
title "ACF/PACF of Colorado Flow 12th Differences";
proc timeseries data=flow seasonality=12 plots=(acf pacf);
  var flow / sdif=1;
run;
*I have not figured out how to print this plot for more lags;
*This is equivalent to; *proc timeseries data=flow plots=(acf pacf); *var flow / dif=12; *run;
title "ARIMA(1,0,0)x(0,1,1)12 2 Year Forecast";
proc arima data=flow;
  identify var=flow(12) nlag=60;
  estimate p=1 q=(12);
  forecast lead=24 id=t out=forecastflow;
run; quit;
title "Monthly Means Plot for Flow";
data flow;
   set flow;
   if mod(_N_,12)=0 then month=12; else month=mod(_N_,12);
run;
proc sort data=flow; by month; run;
proc means data=flow;
  var flow;
  by month;
  output out=flowmeans mean=flowmean;
run;
axis1 label=(angle=90 justify=left "Mean");
proc gplot data=flowmeans; 
  plot flowmean*month / vaxis=axis1;
  symbol v=dot i=join;
run; quit;
proc sort data=flow; by t; run;
*You may also obtain this plot through proc timeseries, but you must have or create a formatted date variable 
for the id statement.  Because our data does not include exact dates, I will create false dates;
data flow;
  set flow;
  if mod(_N_,12)=0 then do; month=12; year=1899+t/12; end; else do; month=mod(_N_,12); year=1900+int(t/12); end;
  sasdate=mdy(month,1,year);
  format sasdate MMDDYY10.; 
run;
proc sort data=flow; by sasdate; run;
proc timeseries data=flow out=series outseason=season;
  id sasdate interval=month accumulate=avg; 
  var flow;
run;
proc sgplot data=season;
  series x=_season_ y=mean;
  xaxis values=(1 to 12 by 1);
run;

* beer dataset analysis;
data beer;
   infile 'C:\Users\Documents\My SAS Files\TS_SAS\Data\beerprod.dat' dlmstr=' ';
   input beer @@ ;
   t=_N_; *because we do not have dates in our data;
   if mod(_N_,4)=0 then quarter=4; else quarter=mod(_N_,4);
run;
title "ACF/PACF of Beer Production 1st & 4th Differences";
proc timeseries data=beer seasonality=4 plots=(acf pacf);
  var beer / dif=1 sdif=1;
run;
title "ARIMA(1,1,1)x(0,1,1)4";
proc arima data=beer;
  identify var=beer(1,4);
  estimate p=(1) q=(1)(4) noint;
run; quit;




