data emeas;
   infile 'C:\Users\OneDrive\Documents\STAT_PSU\STAT510\SAS501\Data\econmeasure.dat' dlmstr=' ';
   input y @@;
   t=_N_; *because we do not have dates in our data;
run;
data epred;
   infile 'C:\Users\OneDrive\Documents\STAT_PSU\STAT510\SAS501\Data\econpredictor.dat' dlmstr=' ';
   input x @@;
   t=_N_; *because we do not have dates in our data;
run;
data econ;
  merge emeas epred;
  by t;
run;
title "Measure vs. Predictor";
axis1 label=(angle=90 justify=left "Measure");
proc gplot data=econ;
   symbol i=none v=circle h=2;
   plot y*x / vaxis=axis1;
run; quit;
title "1st Ordinary Regression";
proc reg data=econ;
  model y = x / r;
  output out=eres r=residuals;
run; quit;
title "ACF/PACF of Residuals";
proc timeseries data=eres plots=(acf pacf);
  var residuals;
run;
title "Residuals ARIMA(1,0,0)";
proc arima data=eres;
  identify var=residuals;
  estimate p=1 noint;
run; quit;
data adjreg;
  set econ;
  ynew=y-.56776*lag(y);
  xnew=x-.56776*lag(x);
run;
title "Adjustment Regression";
proc reg data=adjreg;
  model ynew = xnew / r;
  output out=eadjres r=residuals;
run; quit;
title "ACF/PACF of Residuals";
proc timeseries data=eadjres plots=(acf pacf);
  var residuals;
run;

data y;
   infile 'C:\Users\OneDrive\Documents\STAT_PSU\STAT510\SAS501\Data\l8.1.y.dat' dlmstr=' ';
   input y @@;
   t=_N_; *because we do not have dates in our data;
run;
data x;
   infile 'C:\Users\OneDrive\Documents\STAT_PSU\STAT510\SAS501\Data\l8.1.x.dat' dlmstr=' ';
   input x @@;
   t=_N_; *because we do not have dates in our data;
run;
data ex2;
  merge y x;
  by t;
run;
title "Example 2";
axis1 label=(angle=90 justify=left "Y");
proc gplot data=ex2;
   symbol i=join v=circle h=2;
   plot y*x / vaxis=axis1;
run; quit;
title "1st Ordinary Regression";
proc reg data=ex2;
  model y = x / r;
  output out=res2 r=residuals;
run; quit;

proc print data=res2;
run;quit;
title "ACF/PACF of Residuals";
proc timeseries data=res2 plots=(acf pacf);
  var residuals;
run;
title "Adjustment Regression";
proc autoreg data=ex2;
  model y = x / p=1;
  output out=adjres2 r=residuals;
run; quit;
title "ACF/PACF of Residuals";
proc timeseries data=adjres2 plots=(acf pacf);
  var residuals;
run;

data varve;
   infile 'C:\Users\OneDrive\Documents\STAT_PSU\STAT510\SAS501\Data\varve.dat' dlmstr=' ';
   input varve @@;
   t=_N_; *because we do not have dates in our data;
   tsq=t*t;
   lvarve=log10(varve);
run;
proc means data=varve; 
var t; 
run;
data varve;
  set varve;
  where t < 456; *because this analysis is for illustrative purposes, we reduced 
  the data to the first 455 observations that perform nicely.  As noted in 
  Lesson 8.1, better models exist for this data; 
  ct=t-228;  *center t;
  ctsq=ct*ct;
run;
title "Glacial Varve";
axis1 label=(angle=90 justify=left "Glacial Varve");
proc gplot data=varve;
   symbol i=join v=circle h=2;
   plot lvarve*t / vaxis=axis1;
run; quit;
title "1st Ordinary Regression";
proc reg data=varve;
  model lvarve = ct ctsq / r;
  output out=vres r=residuals;
run; quit;
title "ACF/PACF of Residuals";
proc timeseries data=vres plots=(acf pacf);
  var residuals;
run;
title "Adjustment Regression";
proc autoreg data=varve;
  model lvarve = ct ctsq / p=1;
  output out=vadjres r=residuals;
run; quit;
title "ACF/PACF of Residuals";
proc timeseries data=vadjres plots=(acf pacf);
  var residuals;
run;
